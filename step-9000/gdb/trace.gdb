# Trace runner for step-9000. Input is generated by Make into .trace.input.f.

set logging file trace.md
set logging overwrite on
set logging redirect on
set logging enabled on
set confirm off

echo # JonesForth Step-9000 GDB Session \n

echo # Setup \n
echo ``` \n
break _start
run < .trace.input.f
set pagination off
echo ``` \n

echo \n
echo # `info files` \n
echo ``` \n
info files
echo ``` \n

echo \n
echo # `info proc mappings` \n
echo ``` \n
info proc mappings
echo ``` \n

source ../python-gdb-ui/gdb_program_ui.py
ui-sections

echo \n
echo # Single Stepping \n
echo ``` \n
set $n = 1000
while $n > 0 && $_isvoid($_exitcode)
    echo --------------------\n
    stepi
    echo \n
    if $_isvoid($_exitcode)
        echo $pc \
        x/i $pc
        ui-reg eax
        ui-reg esi
        echo $esp \
        x/4dw $esp
    end
    set $n = $n - 1
end
echo ``` \n

set logging enabled off
quit
